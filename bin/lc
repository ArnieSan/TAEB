#!/usr/bin/env perl
use strict;
use warnings;
use File::Next;

my @files;
my $iter = File::Next::files('lib/', 'bin/');

while (defined (my $file = $iter->())) {
    open my $handle, '<', $file
        or die "Unable to open $file for reading: $!";

    my $lines      = 0;
    my $with_doc   = 0;
    my $with_blank = 0;
    while (<$handle>) {
        ++$with_blank;
        next if /^\s*$/;

        ++$with_doc;
        next if /^=head/ .. /^=cut/;
        next if /^\s*#/;

        ++$lines;
    }

    push @files, [$file, $lines, $with_doc, $with_blank];
}

@files = sort {
    $a->[1] <=> $b->[1] || $a->[2] <=> $b->[2] || $a->[3] <=> $b->[3]
} @files;

my $total_lines = 0;
my $total_doc   = 0;
my $total_blank = 0;
my $length      = 0;

my @out;

for (@files) {
    my ($file, $lines, $with_doc, $with_blank) = @$_;

    $total_lines += $lines;
    $total_doc   += $with_doc;
    $total_blank += $with_blank;

    my $out = sprintf "%7d %7d %7d  %s\n", $lines, $with_doc, $with_blank, $file;
    push @out, $out;

    $length = length($out)
        if length($out) > $length;

}

print "  lines    +doc  +blank  file\n";
printf "%s\n", '-' x $length;
print @out;
printf "%s\n", '-' x $length;
printf "%7d %7d %7d\n", $total_lines, $total_doc, $total_blank;

